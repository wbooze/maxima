/* bhabha1-test.mac

   high energy limit bhabha scattering
   
   */




 invar(D(p1,p1) = 0,D(p1,p2) = s/2,D(p1,p3) = (-t)/2,D(p1,p4) = (-u)/2,
             D(p2,p2) = 0,D(p2,p3) = (-u)/2,D(p2,p4) = (-t)/2,D(p3,p3) = 0,
             D(p3,p4) = s/2,D(p4,p4) = 0);
 done;
 
 M1n:factor(Con(tr(p3,mu,p1,nu)*tr(p2,mu,p4,nu),mu,nu));
 8*(u^2+s^2);
 
 M2n:factor(Con(tr(p2,mu,p1,nu)*tr(p3,mu,p4,nu),mu,nu));
 8*(u^2+t^2);
 
 M12n:factor(tr(p3,mu,p1,nu,p2,mu,p4,nu));
 -8*u^2;
 
 M21n:factor(tr(p2,mu,p1,nu,p3,mu,p4,nu));
 -8*u^2;
 
 MfiSQ:pullfac((-M21n)/(t*s)+(-M12n)/(t*s)+M2n/s^2+M1n/t^2,8);
 8*((u^2+t^2)/s^2+(u^2+s^2)/t^2+2*u^2/(s*t));
 
 (assume(E > 0,th >= 0,th <= %pi),
        comp_def(p1(E,0,0,E),p2(E,0,0,-E),p3(E,E*sin(th),0,E*cos(th)),
                p4(E,-E*sin(th),0,-E*cos(th))),
         s_th:VP(p2+p1,p2+p1));
 4*E^2;
 
 t_th:factor(VP(p1-p3,p1-p3));
 2*(cos(th)-1)*E^2;
 
 u_th:factor(VP(p1-p4,p1-p4));
 -2*(cos(th)+1)*E^2;
 
  /* convert MfiSq to function of th */
 
  MfiSQ_th:(sub_stu(MfiSQ),factor(%%));
  4*(cos(th)^2+3)^2/(cos(th)-1)^2;
  
  t_th2:to_ao2(t_th,th);
 -4*sin(th/2)^2*E^2;
 
  /* Dirac spinor methods for polarized amplitudes */
  
  /* case helicities (1,1) --> (1,1)  */
 
  ( up1:UU(E,E,0,0,1),
         up3b:sbar(UU(E,E,th,0,1)),
         vp2b:sbar(VV(E,E,%pi,0,1)),
         vp4:VV(E,E,%pi-th,%pi,1),
         a13:up3b . Gam[mu] . up1,
         a42:vp2b . Gam[mu] . vp4,
         mcon(a13*a42,mu),
         Mt : expand (trigsimp (%%)));
   -8*E^2;
   
   M1:Mt/t_th2;
   2/sin(th/2)^2;
   
   Ms:(a12:vp2b . Gam[_mu%] . up1,a43:up3b . Gam[_mu%] . vp4,
              mcon(a12*a43,_mu%),expand(trigsimp(%%)));
   0;
   
   M2:Ms/s_th;
   0;
   
   M1-M2;
   2/sin(th/2)^2;
   
   
   ( he_me(sp1v,sp2v,sp3v,sp4v):=
          block([up1,up2,up3b,up4b,_mu%,
                  a13,a42,Mt,Ms,M1,M2],
             up1:UU(E,E,0,0,sp1v),
             up3b:sbar(UU(E,E,th,0,sp3v)),
             vp2b:sbar(VV(E,E,%pi,0,sp2v)),
             vp4:VV(E,E,%pi-th,%pi,sp4v),
             a13:up3b . Gam[_mu%] . up1,
             a42:vp2b . Gam[_mu%] . vp4,
             mcon(a13*a42,_mu%),
             Mt : expand (trigsimp (%%)),
             M1:Mt/t_th2,
             a12:vp2b . Gam[_mu%] . up1,
             a43:up3b . Gam[_mu%] . vp4,
             mcon(a12*a43,_mu%),
             Ms : expand(trigsimp(%%)),
             M2:Ms/s_th,                        
             M1 - M2 ),
          he_me(1,1,1,1) );
   2/sin(th/2)^2;
   
   /* calc global mssq = sum of absolute value squared
       of polarized amplitudes   */
   
   block([sL,s1,s2,s3,s4,temp],sL:[1,-1],mssq:0,
              for s1 in sL do
                  for s2 in sL do
                      for s3 in sL do
                          for s4 in sL do
                              (temp: he_me(s1,s2,s3,s4),
                               mssq:Avsq(temp)+mssq),
              mssq:expand(fr_ao2(mssq,th)));
  4*cos(th)^4/(cos(th)^2-2*cos(th)+1)
        +24*cos(th)^2/(cos(th)^2-2*cos(th)+1)+36/(cos(th)^2-2*cos(th)+1);
   
 
   /* compare with unpolarized amplitude squared
      calculated using trace methods */
 
  trigsimp(mssq-MfiSQ_th);
  0;
  
  